buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("au.com.dius:pact-jvm-provider-gradle:4.0.3")
	}
}

plugins {
	id 'org.springframework.boot' version '2.2.2.RELEASE'
	id 'io.spring.dependency-management' version '1.0.8.RELEASE'
	id 'java'
	id 'au.com.dius.pact' version "4.0.4"
	id 'com.wiredforcode.spawn' version '0.7.0'

}

group = 'se.frusunnanbo'
version = '1.0.0'
sourceCompatibility = '13'

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
}

test {
	useJUnitPlatform()
}

task startApplication(type: SpawnProcessTask, dependsOn: 'bootJar') {
	command "java -jar $buildDir/libs/${jar.archiveFileName.get()}"
    ready 'Tomcat started on port(s):'
}

task killApplication(type: KillProcessTask)

pact {
	serviceProviders {
		animal_api {

			startProviderTask = startApplication
			terminateProviderTask = killApplication

			protocol = 'http'
			host = 'localhost'
			port = 8080
			path = '/'

			hasPactWith('Animal Visualizer') {
				pactSource = file("$rootDir/pacts/animal_visualiser-animal_api.json")
			}

			// Test Pacts from Pact Broker
			//hasPactsFromPactBroker(mybrokerUrl, authentication: ['Basic', mybrokerUser, mybrokerPassword])

		}
	}
}
