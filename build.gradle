buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("au.com.dius:pact-jvm-provider-gradle:4.0.3")
	}
}

plugins {
	id 'org.springframework.boot' version '2.2.2.RELEASE'
	id 'io.spring.dependency-management' version '1.0.8.RELEASE'
	id 'java'
	id 'au.com.dius.pact' version "4.0.4"
	id 'com.wiredforcode.spawn' version '0.7.0'
}

static def currentGitHash() {
	'git rev-parse --verify --short HEAD'.execute().text.trim()
}

group = 'se.frusunnanbo'
version = "1.0.0-${currentGitHash()}".toString()

java {
	sourceCompatibility = JavaVersion.VERSION_13
	targetCompatibility = JavaVersion.VERSION_13
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'commons-io:commons-io:2.6'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
}

test {
	useJUnitPlatform()
}

task startApplication(type: SpawnProcessTask, dependsOn: 'bootJar') {
	command "java -jar -Dserver.port=8081 $buildDir/libs/${jar.archiveFileName.get()} "
	ready 'Tomcat started on port(s):'
}

task killApplication(type: KillProcessTask)

def pactBrokerToken = Project.hasProperty('pactBrokerToken') ? pactBrokerToken : System.getenv('PACT_BROKER_TOKEN')
def pactBrokerUrl = 'https://frusunnanbo.pact.dius.com.au'

pact {

	serviceProviders {
		'Service C' {

			startProviderTask = startApplication
			terminateProviderTask = killApplication

			protocol = 'http'
			host = 'localhost'
			port = 8081
			path = '/'

			hasPactsFromPactBroker(pactBrokerUrl, authentication: ['Bearer', pactBrokerToken]) {
				stateChangeUrl = url('http://localhost:8081/pactStateChange')
				stateChangeUsesBody = true
			}
		}
	}

}

